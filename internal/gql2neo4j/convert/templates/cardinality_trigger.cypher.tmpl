{{- if or (gt .SourceLowerBound 0) (gt .SourceUpperBound 0) -}}
UNWIND
    $createdNodes +
    [r IN $createdRelationships WHERE type(r) = '{{.RelationshipType}}' | startNode(r)] +
    [r IN $deletedRelationships WHERE type(r) = '{{.RelationshipType}}' | startNode(r)]
    AS node
WITH DISTINCT node
WHERE '{{.SourceNodeType}}' IN labels(node)
    AND node IS NOT NULL
OPTIONAL MATCH (node)-[r:{{.RelationshipType}}]->()
WITH node, count(r) AS relCount
CALL apoc.util.validate(
    relCount < {{.SourceLowerBound}} OR relCount > {{.SourceUpperBound}},
    '{{.SourceNodeType}} {{.RelationshipType}} constraint violated',
    []
)
RETURN NULL
{{if or (gt .DestinationLowerBound 0) (gt .DestinationUpperBound 0)}}

UNION

{{end}}{{end}}
{{- if or (gt .DestinationLowerBound 0) (gt .DestinationUpperBound 0) -}}
UNWIND
    $createdNodes +
    [r IN $createdRelationships WHERE type(r) = '{{.RelationshipType}}' | endNode(r)] +
    [r IN $deletedRelationships WHERE type(r) = '{{.RelationshipType}}' | endNode(r)]
    AS node
WITH DISTINCT node
WHERE '{{.DestinationNodeType}}' IN labels(node)
    AND node IS NOT NULL
OPTIONAL MATCH ()-[r:{{.RelationshipType}}]->(node)
WITH node, count(r) AS relCount
CALL apoc.util.validate(
    relCount < {{.DestinationLowerBound}} OR relCount > {{.DestinationUpperBound}},
    '{{.DestinationNodeType}} {{.RelationshipType}} constraint violated',
    []
)
RETURN NULL
{{- end}}
